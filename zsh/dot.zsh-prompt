###
#  .zsh/prompt - ZSH Prompt
##
# Adapted from code found at <https://gist.github.com/1712320>.
###

setopt prompt_subst

autoload -Uz colors && colors

# vcs_info
VCS_NAME="%{$fg[cyan]%}%s%{$reset_color%}"
VCS_ACTION="%{$fg[red]%}%a%{$reset_color%}"
VCS_PROMPT="%{$fg[cyan]%}>%{$reset_color%}"
VCS_BRANCH="%{$fg[white]%}%b%{$reset_color%}"
VCS_STAGED="%{$fg[green]%}✘%{$reset_color%}"
VCS_UNSTAGED="%{$fg[yellow]%}✘%{$reset_color%}"
VCS_UNTRACKED="%{$fg[red]%}✘%{$reset_color%}"
VCS_BRL="%{$fg[cyan]%}[%{$reset_color%}"
VCS_BRR="%{$fg[cyan]%}]%{$reset_color%}"

autoload -Uz vcs_info

zstyle ':vcs_info:*' enable hg git svn cvs

zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' get-revision true

zstyle ':vcs_info:*' formats "${VCS_NAME}${VCS_PROMPT} $VCS_BRL$VCS_BRANCH%m%u%c$VCS_BRR "
zstyle ':vcs_info:*' actionformats "${VCS_NAME}:${VCS_ACTION}${VCS_PROMPT} $VCS_BRL$VCS_BRANCH%m%u%c$VCS_BRR "
zstyle ':vcs_info:*' stagedstr " $VCS_STAGED"
zstyle ':vcs_info:*' unstagedstr " $VCS_UNSTAGED"

# Mercurial
zstyle ':vcs_info:hg*:*' get-bookmarks true
zstyle ':vcs_info:hg*:*' get-mq true
zstyle ':vcs_info:hg*:*' get-unapplied true
zstyle ':vcs_info:hg*:*' patch-format "mq(%g):%n/%c %p"
zstyle ':vcs_info:hg*:*' nopatch-format "mq(%g):%n/%c %p"
zstyle ':vcs_info:hg*:*' hgrevformat "%r"
zstyle ':vcs_info:hg*:*' branchformat "%b"

# hg untracked
zstyle ':vcs_info:hg*+set-message:*' hooks hg-untracked
+vi-hg-untracked() {
if hg status | grep '^?' &> /dev/null; then
	hook_com[staged]+=" $VCS_UNTRACKED"
fi
}

# git untracked
zstyle ':vcs_info:git*+set-message:*' hooks git-untracked
+vi-git-untracked() {
if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
	git status --porcelain | grep '??' &> /dev/null ; then
	hook_com[staged]+=" $VCS_UNTRACKED"
fi
}

HOST_COLOR="$hcolor"
CDIR_COLOR="%{$fg[yellow]%}"
if [ "`whoami`" = "root" ]
then
	USER_COLOR="%{$fg[red]%}"
	PROMPT_COLOR="%{$fg[red]%}"
else
	USER_COLOR="$HOST_COLOR"
	PROMPT_COLOR="%{$fg[white]%}"
fi

precmd() {
	((PWDLEN = $COLUMNS - ${#${(%):-%n-%m------(-)}}))
	vcs_info
}

PR_HOST="$HOST_COLOR%n%{$reset_color%}"
PR_USER="$USER_COLOR%m%{$reset_color%}"
PR_PROMPT="$PROMPT_COLOR%#%{$reset_color%}"
PR_RETCODE="%(?.. < %{$fg_bold[red]%?$pr_reset >)"
PR_TIMEDATE="[]"

PROMPT='~>$PR_RETCODE ${PR_HOST} @ ${PR_USER} $CDIR_COLOR%$PWDLEN<...<%~%<<%{$reset_color%}
 $vcs_info_msg_0_$PR_PROMPT '
PROMPT2='(%_) $hcolor>$pr_reset '
RPROMPT=''
